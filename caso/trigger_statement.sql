CREATE OR REPLACE PACKAGE PKG_TRIGGER_COUNTERS AS
  PROCEDURE reset_counter;
  PROCEDURE inc_counter;
  FUNCTION get_counter RETURN NUMBER;
END PKG_TRIGGER_COUNTERS;
/

CREATE OR REPLACE PACKAGE BODY PKG_TRIGGER_COUNTERS AS
  g_counter NUMBER := 0;

  PROCEDURE reset_counter IS
  BEGIN
    g_counter := 0;
  END reset_counter;

  PROCEDURE inc_counter IS
  BEGIN
    g_counter := g_counter + 1;
  END inc_counter;

  FUNCTION get_counter RETURN NUMBER IS
  BEGIN
    RETURN NVL(g_counter, 0);
  END get_counter;
END PKG_TRIGGER_COUNTERS;
/

BEGIN
  EXECUTE IMMEDIATE 'CREATE TABLE MATRICULAS_BATCH_LOG (
    LOG_ID NUMBER GENERATED BY DEFAULT ON NULL AS IDENTITY PRIMARY KEY,
    EVENT_TYPE VARCHAR2(20),
    AFFECTED_ROWS NUMBER,
    CREATED_AT TIMESTAMP DEFAULT SYSTIMESTAMP,
    USERNAME VARCHAR2(30)
  )';
EXCEPTION
  WHEN OTHERS THEN
    IF SQLCODE = -955 THEN -- ORA-00955: name is already used by an existing object
      NULL;
    ELSE
      RAISE;
    END IF;
END;
/

CREATE OR REPLACE TRIGGER trg_matriculas_before_insert_stmt
BEFORE INSERT ON MATRICULAS
DECLARE
BEGIN
  PKG_TRIGGER_COUNTERS.reset_counter;
END;
/

CREATE OR REPLACE TRIGGER trg_matriculas_after_insert_row
AFTER INSERT ON MATRICULAS
FOR EACH ROW
BEGIN
  PKG_TRIGGER_COUNTERS.inc_counter;
END;
/

CREATE OR REPLACE TRIGGER trg_matriculas_after_insert_stmt
AFTER INSERT ON MATRICULAS
DECLARE
  v_cnt NUMBER;
BEGIN
  v_cnt := PKG_TRIGGER_COUNTERS.get_counter;

  INSERT INTO MATRICULAS_BATCH_LOG (EVENT_TYPE, AFFECTED_ROWS, CREATED_AT, USERNAME)
  VALUES ('INSERT', v_cnt, SYSTIMESTAMP, SYS_CONTEXT('USERENV','SESSION_USER'));

EXCEPTION
  WHEN OTHERS THEN
    NULL;
END;
/
